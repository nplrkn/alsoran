%import common.WS
%import common.DIGIT

COMMENT: (/--[^\n]*--/ | /--[^\n]*\n/) 
NEWLINE: "\n"
%ignore WS
%ignore COMMENT
%ignore NEWLINE

IDENTIFIER:       ("a".."z" | "A".."Z" | DIGIT | "-")+
_IDENTIFIER:       ("a".."z" | "A".."Z" | DIGIT | "-")+
INTEGER:          "-"? "0" | "1".."9"("0".."9")*
BOUND:            INTEGER | IDENTIFIER
_optional:        "OPTIONAL" | "optional" | "conditional"
extension_marker: "..." [","]

// For testability, we make the parts before and after the definitions optional
// This means we can just run small fragments through the grammar.
document:         [module _OID _begin ["IMPORTS" _IMPORTS]] _definitions ["END"]
module:           IDENTIFIER
_OID:             /{[^}]+}/
_begin:           "DEFINITIONS AUTOMATIC TAGS ::=" "BEGIN" 
_IMPORTS:         /[^;]+;/
_definitions:     (tuple_struct | struct | enumdef | choicedef | constantdef | objectdef)+

?assign:          IDENTIFIER "::="
tuple_struct:     assign _primitive
struct:           assign sequence
enumdef:          assign enumerated
choicedef:        assign choice
constantdef:      IDENTIFIER _IDENTIFIER "::=" INTEGER
objectdef:        IDENTIFIER _identifier "::=" ies

_items{x}:        "{" _comma_separated{x}? (extension_marker extended_items{x}?)? "}" 
extended_items{x}: _comma_separated{x}

sequenceof:       "SEQUENCE" [_size] "OF" (IDENTIFIER | container)
sequence:         "SEQUENCE" _items{_fielditem}
enumerated:       "ENUMERATED" _items{enum_item}
choice:           "CHOICE" _items{_choiceitem}
integer:          "INTEGER" namedvalues? ["(" _ranges ")"]
printablestring:  "PrintableString" [_size]
visiblestring:    "VisibleString" [_size]
utf8string:       "UTF8String" [_size]
bits.2:           "BIT STRING" [_size]
bytes.2:          ("OCTET STRING" [_size | "(CONTAINING" IDENTIFIER ")"]) | "OBJECT IDENTIFIER"
boolean:          "BOOLEAN"
_identifier:      IDENTIFIER
_inttype:         IDENTIFIER
_objecttype:      IDENTIFIER

_primitive:     sequenceof
                | integer
                | printablestring
                | visiblestring
                | utf8string
                | bits 
                | bytes
                | boolean
                | _identifier

ies:              "{" ie* [","] extension_marker? "}"
ie:               "{" "ID" "id-"_identifier "CRITICALITY" ("reject" | "ignore") ("TYPE" | "EXTENSION") _fieldtype + "PRESENCE" (_optional | "mandatory") "}" ["|"]

_comma_separated{x}: x ("," x)* ","?
_choiceitem:     choicefield | extension_container
choicefield:      _identifier _fieldtype
_fielditem:       optional_field | field | extension_container
enum_item:        _identifier
extended_enum_items: enum_item+ -> extended_items
extension_container:  _identifier container "OPTIONAL"? ","?
container:        _identifier "{" "{" _identifier "}" "}" 
optional_field.2:   _identifier _fieldtype "OPTIONAL"
field.1:           _identifier _fieldtype
_fieldtype:       _primitive | enumerated | sequence
_nestedbraces:    "{" (_nestedbraces|/[^\{\}]+/) * "}"

_size:            "(" "SIZE" "(" _ranges ")" ")"
_ranges:          _rangeoptions+
_rangeoptions:    _range["|" _range] * [","]
_range:           extension_marker | (BOUND [".." BOUND])        

namedvalues:     _nestedbraces


